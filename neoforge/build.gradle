import com.modrinth.minotaur.dependencies.ModDependency

plugins {
    id 'dev.architectury.loom'
    id 'architectury-plugin'
    id 'com.github.johnrengelman.shadow'
    id "com.modrinth.minotaur"
}

architectury {
    platformSetupLoomIde()
    neoForge()
}

loom {
    
}

configurations {
    common
    shadowCommon
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentNeoForge.extendsFrom common
}

dependencies {
    minecraft("com.mojang:minecraft:${rootProject.minecraft_version}")
    mappings(loom.officialMojangMappings())

    neoForge("net.neoforged:neoforge:${rootProject.neoforge_version}")

    modImplementation("dev.architectury:architectury-neoforge:${rootProject.architectury_version}")

    common(project(path: ':common', configuration: 'namedElements')) { transitive false }
    shadowCommon(project(path: ':common', configuration: 'transformProductionNeoForge')) { transitive false }

    runtimeOnly files("../testdata/resourcepack")
    runtimeOnly files("../testdata/datapack")
}

processResources {
    var properties = [
        mod_version     : project.version,
        minecraft_version: rootProject.minecraft_version,
        neoforge_version: rootProject.neoforge_version
    ]
    inputs.properties(properties)
    filesMatching('META-INF/neoforge.mods.toml') {
        expand(properties)
    }
}

shadowJar {
    configurations = [project.configurations.shadowCommon]
    archiveClassifier = 'dev-shadow'
}

remapJar {
    input.set(shadowJar.archiveFile)
    dependsOn(shadowJar)
    archiveClassifier = ''
}

jar.archiveClassifier = 'dev'
sourcesJar.archiveClassifier = 'dev-sources'

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}

loom {
    
}

modrinth {
    token = System.getenv('MODRINTH_TOKEN')
    projectId = "${project.modrinth_project_id}"
    versionNumber = "v${project.mod_version}-mc${project.minecraft_version}-neoforge"
    versionName = "${project.mod_version}-NeoForge for Minecraft v${project.minecraft_version}"
    uploadFile = remapJar
    gameVersions = [project.minecraft_version]
    loaders = ['neoforge']
    dependencies = [
        new ModDependency('lhGA9TYQ', 'required')
    ]
}
